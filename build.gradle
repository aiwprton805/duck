plugins {
    id 'war'
    id 'eclipse'
}

eclipse {
    classpath {
        downloadJavadoc = true
        downloadSources = true
    }
}

repositories {
    jcenter()
}

dependencies {
    /* ============================ DATABASE ============================*/
    compile group: 'org.postgresql', name: 'postgresql', version: '42.2.5'
    compile group: 'org.apache.commons', name: 'commons-dbcp2', version: '2.5.0'
    compile group: 'org.mybatis', name: 'mybatis-spring', version: '1.3.2'
    compile group: 'org.mybatis', name: 'mybatis', version: '3.4.6'
    
    /* ============================= SPRING =============================*/
    compile group: 'org.springframework', name: 'spring-context', version: '5.1.0.RELEASE'
    compile group: 'org.springframework', name: 'spring-webmvc', version: '5.1.0.RELEASE'
    compile group: 'org.springframework', name: 'spring-tx', version: '5.1.0.RELEASE'
    compile group: 'org.springframework', name: 'spring-jdbc', version: '5.1.0.RELEASE'
    
    /* ========================= SPRING SECURITY ========================*/
    compile group: 'org.springframework.security', name: 'spring-security-web', version: '5.1.0.RELEASE'
    compile group: 'org.springframework.security', name: 'spring-security-config', version: '5.1.0.RELEASE'
    
    /* ============================= SERVLET ============================*/
    compile group: 'javax.servlet', name: 'jstl', version: '1.2'
    providedCompile group: 'javax.servlet', name: 'javax.servlet-api', version: '3.1.0'
    providedCompile group: 'javax.servlet.jsp', name: 'javax.servlet.jsp-api', version: '2.3.3'
    
    /* ============================= LOGGING ============================*/
    compile group: 'ch.qos.logback', name: 'logback-classic', version: '1.2.3'
    
    /* ============================== TEST ==============================*/
    testCompile group: 'junit', name: 'junit', version: '4.12'
}

    /* ================= CUSTOM PROCESS RESOURCES TASK ================= */
processResources {
    doLast {
        def projectProperties = new Properties()
        projectProperties.load(new FileInputStream("${project.projectDir}/project.properties"))
        copy {
            from("src/main/resources") {
                include "**/*.properties"
                include "**/*.xml"
                include "**/*.sql"
                exclude "**/logback*" //custom logback.xml path
                filter(org.apache.tools.ant.filters.ReplaceTokens, tokens: projectProperties,
                    beginToken: '@{', endToken: '}')
            }
            into "${project.buildDir}/resources/main"
        }
        //custom logback.xml path
        copy {
            from "src/main/resources/config/logback.xml"
            into "${project.buildDir}/resources/main"
        }
    }
}
